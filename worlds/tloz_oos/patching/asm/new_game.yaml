# setting up a new file - this is done when link is dropped into the world, not
# at actual file creation.

# Set default text speed to 5
07/0183/: db $04

# Change Link starting point
07/0193/: db option.startingGroup       # Room
07/0195/: db option.startingRoom        # Room
07/0197/: db option.startingPosY        # Y
07/0199/: db option.startingPosX        # X
07/01a6/: db option.animalCompanion     # Natzu layout

# Set new game max bombs to 0 instead of 10
07/018d/: db $00

# overwrite initial health and max health (since they are overwritten by another function anyway) to set
# the initial seed selection instead. first byte is low c6xx address, second is index.
07/018e/satchelInitialSelection: db $be, option.defaultSeedType - $20
07/019a/slingshotInitialSelection: db $bf, option.defaultSeedType - $20

# On file creation, set additional flags to skip a few events/cutscenes/tutorials
# from the vanilla game which are cumbersome in a rando context.
07//setInitialFlags: |
  ; No linked gaming
  xor a
  ld (wIsLinkedGame),a

  ; Mark intro as seen
  ld a,$0a
  call setGlobalFlag
  ld a,$1c
  call setGlobalFlag

  ; Animal vars
  ld a,$ff
  ld (wAnimalTutorialFlags),a
  
  ; Starting Maps & Compasses
  ld a,option.startingMapsCompasses
  or a
  jr z,@skipMapsCompasses
  ld a,$ff
  ld hl,wDungeonCompasses
  ldi (hl),a
  ldi (hl),a
  ldi (hl),a
  ld (hl),a
  @skipMapsCompasses:
  
  ; Remove the requirement to go in the screen under Sunken City tree to make Dimitri bullies appear
  ld a,$20
  ld (wDimitriState),a

  ; Change seeds selected by default inside slingshot and satchel
  ld a,option.defaultSeedType
  ld hl,wSatchelSelectedSeeds
  ldi (hl),a    ; Satchel initial selection
  ld (hl),a     ; Slingshot initial selection

  ; Room flag 6
  ld a,$40
  ld ($c796),a   ; Remove post-dungeon cutscene at D1 entrance
  ld ($c78d),a   ; Remove post-dungeon cutscene at D2 entrance
  ld ($c760),a   ; Remove post-dungeon cutscene at D3 entrance
  ld ($c71d),a   ; Remove post-dungeon cutscene at D4 entrance
  ld ($c78a),a   ; Remove post-dungeon cutscene at D5 entrance
  ld ($c700),a   ; Remove post-dungeon cutscene at D6 entrance
  ld ($c7d0),a   ; Remove post-dungeon cutscene at D7 entrance
  ld ($c800),a   ; Remove post-dungeon cutscene at D8 entrance
  ld ($c829),a   ; Remove Maku Tree cutscene at temple of seasons gate
  ld ($c82a),a   ; Remove Maku Tree cutscene at winter tower
  ld ($c79b),a   ; Sokra stump
  ld ($c7e9),a   ; Sokra in town
  ld ($c7a7),a   ; Vanilla start room

  ; Room flags 5 | 6 | 7
  ld a,$e0
  ld ($c79a),a  # Rosa portal

  ; Room flags 6 | 7
  ld a,$c0
  ld ($c798),a  # Troupe
  ld ($c7cb),a  # Rosa (first encounter)

  ; Room flag 0
  ld a,$01
  ld ($c716),a  ; Enable a tile replacement in Temple Remains to prevent softlock when exiting cave

  ; Give L-3 ring box
  ld a,$10
  ld ($c697),a  # treasure flag
  ld a,$03
  ld (wRingBoxLevel),a

  # automatically open tarm gate if requirement is 0 jewels
  ld a,option.tarmGateRequiredJewels
  or a
  jr nz,@done
  ld a,$80
  ld ($c763),a

  @done:
  ld hl,$4182  ; initialFileVariables
  ret
07/000a/: call setInitialFlags

# Remove the "Accept our quest, hero!" cutscene when launching the game
# for the first time
01/1874/: |
  nop
  nop
  xor a
  cp $01

# Disable Impa intro script by jumping directly to @impaIntroDone
0b/34aa/: db $74,$ef
# Space from 0x2F4AC to 0x2F4EE (included) is free
