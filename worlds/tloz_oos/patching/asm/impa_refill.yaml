# An "adapter" function to change the parameter registers, making it easier to use in a common context
# (similar to checkTreasureObtained from bank 0, but without an interBankCall)
3f//checkTreasureObtained_bodyProxy: |
    push hl
    ld l,a
    call $446d  ; checkTreasureObtained_body
    ld a,l
    srl h
    pop hl
    ret

# Refill all consumable / losable items in the inventory:
#   - each seed type up to 20
#   - all bombs
#   - shield (in case it was eaten by a Like Like)
#   - health
# No new item can be obtained from this, it can only refill what the player had and used/lost at some point.
3f//refillInventory: |
    ld b,TREASURE_EMBER_SEEDS
    ld hl,wNumEmberSeeds
    
    @refillSeedsLoop:
    ld a,b
    call checkTreasureObtained_bodyProxy
    jr nc,@nextSeed
    ld a,(hl)           ; currently owned seeds
    cp $20
    jr nc,@nextSeed
    ld (hl),$20
    
    @nextSeed:
    ld a,b
    cp TREASURE_MYSTERY_SEEDS
    jr z,@refillBombs
    inc hl
    inc b
    jr @refillSeedsLoop
    
    @refillBombs:
    ld a,TREASURE_BOMBS
    call checkTreasureObtained_bodyProxy
    jr nc,@refillShield
    ld hl,wMaxBombs
    ldd a,(hl)
    ld (hl),a
    
    @refillShield:
    ld a,TREASURE_SHIELD
    call checkTreasureObtained_bodyProxy
    jr nc,@refillHealth
    ld a,TREASURE_SHIELD
    ldh ($8b),a         ; put item ID in FF8B
    call $46bc          ; addTreasureToInventory 
    
    @refillHealth:
    ld hl,wLinkMaxHealth
    ldd a,(hl)
    ld (hl),a
    
    ; Play a sound and update status bar to give feedback
    ld a,SND_GETSEED
    call playSound
    ld a,$03
    ld (wStatusBarNeedsRefresh),a
    ret

# A function usable through the `asm15` script command which triggers the refill function above
15//impaRefill: |
    push hl
    push de
    ld e,$3f
    ld hl,refillInventory
    call interBankCall
    pop de
    pop hl
    ret

# Actual use on Impa script of the refill script function above
0b/34ef/: |
    db checkabutton
    db disableinput
    db showtext,$25,$03
    
    db asm15
    dw impaRefill
    
    db enableinput
    db $74,$ef  ; jump back to the beginning of the script

# Change Impa text to explain that she is providing a refill
1f/0247/: |
    db $04,$b6,$04,$06,$05,$a0,$05,$07,$03,$d2,$03,$bd,$61,$01,$72,$65,$66,$69,$6c,$6c,$2e,$00

# Make Like-Like only remove the shield without unsetting the treasure flag
0c//loseShieldWithoutLosingFlag: |
    call loseTreasure
    ld hl,wObtainedTreasureFlags
    set 1,(hl)
    ret
0c/1dd2/: call loseShieldWithoutLosingFlag

# Make Impa always stand in front of her house
0a/1d57/: |
    db $00,$00,$00,$00,$00,$00,$00,$00,$00
    db $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00
    db $00,$00,$00